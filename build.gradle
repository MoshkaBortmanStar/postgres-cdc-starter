plugins {
	id 'java'
	id 'maven-publish'
	id 'org.springframework.boot' version '3.2.4'
	id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.bortmanco'
version = '0.0.1'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	//spring
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-autoconfigure'
	//reactor
	implementation 'io.projectreactor:reactor-core'
	//pg
	implementation 'org.postgresql:postgresql'
	//annotation processor
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java

			pom {
				name = 'spring-boot-starter-postgres-cdc'
				description = 'Spring Boot Starter for Postgres CDC'
				url = 'https://github.com/MoshkaBortmanStarr'

				licenses {
					license {
						name = 'The Apache Software License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}

				developers {
					developer {
						id = '1'
						name = 'Moshka Bortman'
						email = 'mishka.sshishka@gmail.com'
					}
				}

				scm {
					connection = 'scm:git:https://github.com/MoshkaBortmanStar/postgres-cdc-starter.git'
					developerConnection = 'scm:git:ssh://github.com:MoshkaBortmanStar/postgres-cdc-starter.git'
					url = 'https://github.com/MoshkaBortmanStar/postgres-cdc-starter'
				}
			}
		}
	}

	repositories {
		maven {
			name = 'OSSRH'
			url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
			credentials {
				username = project.findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
				password = project.findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
			}
		}
	}
}